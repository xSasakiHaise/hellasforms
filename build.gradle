plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

version = "2.8.0"
group = "com.xsasakihaise.hellasforms"

base {
    archivesName = "hellasforms"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {

    mappings channel: 'official', version: '1.16.5'

    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                "${hellasforms}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', "hellasforms"
        }

        server {
            property 'forge.enabledGameTestNamespaces', "hellasforms"
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', "hellasforms"
        }

        data {
            workingDirectory project.file('run-data')

            args '--mod', "hellasforms", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://repo.lucko.me/' }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://jitpack.io" }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}

def pixelmonJarName = (project.findProperty("pixelmonJarName") ?: "Pixelmon-1.16.5-9.1.13-universal.jar").toString()
def pixelmonJar = file("libs/${pixelmonJarName}")
if (!pixelmonJar.exists()) {
    def pixelmonMatches = fileTree("libs").files
            .findAll { it.name.toLowerCase().contains("pixelmon") && it.name.toLowerCase().endsWith(".jar") }
            .toList()
    if (!pixelmonMatches.isEmpty()) {
        pixelmonJar = pixelmonMatches.sort { it.name }.last()
    }
}
if (!pixelmonJar.exists()) {
    throw new GradleException("Missing Pixelmon jar at libs/${pixelmonJarName}. You can also place any *pixelmon*.jar in libs/ or set -PpixelmonJarName=... to override.")
}

def hellasControlLocalJars = fileTree("libs").files
        .findAll { it.name.toLowerCase().contains("hellascontrol") && it.name.toLowerCase().endsWith(".jar") }
        .toList()
def hellasControlLocalJar = hellasControlLocalJars.isEmpty() ? null : hellasControlLocalJars.sort { it.name }.last()
def useHellasControlLocalJar = hellasControlLocalJar != null && hellasControlLocalJar.exists()


dependencies {
    minecraft "net.minecraftforge:forge:1.16.5-36.2.42"

    // Place Pixelmon at libs/Pixelmon-1.16.5-9.1.12-universal.jar (not tracked in git).
    // You can override the name with -PpixelmonJarName=... or drop a Pixelmon-1.16.5-9.1.1*-universal.jar into libs/.
    implementation fg.deobf(files(pixelmonJar))
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0'
    // If you have hellascontrol checked out next to this repo, includeBuild("../hellascontrol") will be used.
    // Alternatively, drop any *hellascontrol*.jar into libs/ to use that directly.
    if (useHellasControlLocalJar) {
        compileOnly fg.deobf(files(hellasControlLocalJar))
    } else if (file("../hellascontrol").exists()) {
        compileOnly fg.deobf("com.xsasakihaise.hellascontrol:hellascontrol:2.0.0")
    } else {
        compileOnly fg.deobf("com.github.xSasakiHaise:hellascontrol:master-SNAPSHOT")
    }

}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    setArchiveBaseName(rootProject.name + '-Forge')


    exclude 'net.minecraft'
    exclude "**/module-info.class"
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : "HellasForms",
                'Specification-Vendor'    : "xSasaki_Haise",
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : "hellasforms",
                'Implementation-Version'  : "com.xsasakihaise.hellasforms",
                'Implementation-Vendor'   : "xSasaki_Haise",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task versionedRelease(type: Copy) {
    delete fileTree('release/') {
        include '**/*.jar'
    }
    group "build"
    from('./build/com.xsasakihaise.hellasforms.libs/' + rootProject.name + '-Forge.jar')
    into('release/')
    include '*.jar'
    rename { String filename ->
        filename.replace(".jar", "-${project.version}.jar")
    }
}

javadoc {
    options {
        links 'https://reforged.gg/docs/1201/'
    }
}

jar.finalizedBy('shadowJar')
shadowJar.finalizedBy('reobfJar')
build.finalizedBy('versionedRelease')
